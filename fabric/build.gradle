plugins {
    id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
    implementation project(':common')
    include project(':common')
}

test {
    useJUnitPlatform()
}

fabricApi {
    configureTests {
        createSourceSet = true
        modId = "lss-test"
        enableGameTests = true
        enableClientGameTests = true
        eula = true
    }
}

afterEvaluate {
    loom.runs.configureEach {
        if (name.toLowerCase().contains("gametest")) {
            vmArg "-Dlss.test.integratedServer=true"
        }
        if (name.toLowerCase().contains("clientgametest")) {
            vmArg "-Dfabric.client.gametest.disableNetworkSynchronizer=true"
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from(rootProject.file("LICENSE")) {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}
